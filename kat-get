#!/usr/bin/perl
use Mojolicious::Lite;
use Mojo::UserAgent;
use Mojo::DOM;
use Mojo::Util qw(url_escape url_unescape);
use Getopt::Long;

# Parse command line
my $port;
my $daemon;
GetOptions(
    "d|daemon" => \$daemon,
    "p|port=i" => \$port,
    "h|help" => sub {
        print <<EOF;
USAGE
kat-get MotoGP Jerez 720p
kat-get -d
kat-get -p 8080

DESCRIPTION
Torrent search and start from Kickass Torrents, command line or webapp.
In the first form run in CLI mode and search torrents for terms in args,
and start selected torrent.
Start as a webapp with options -d or -p.

OPTIONS
-d	start as webapp, default port 31101
-p port	start as webapp on specified port
-h	print this help
EOF
        exit 0;
    },
);
$daemon = 1 if $port;
$port //= 31101 if $daemon;
my $query = url_escape join(' ', @ARGV);
shift @ARGV while @ARGV;

# URLs
my %urls = (
    rss => "https://kat.cr/usearch/%s/?rss=1",
    html => "https://kickasstop.com/search.php?q=%s",
);
# User agent object
my $ua = Mojo::UserAgent->new;

# Routes
get '/' => sub {
    my $c = shift;
    $c->render(template => 'page', title => 'Torrents');
};

get '/*query' => sub {
    my $c = shift;
    my $in = url_unescape $c->stash('query');
    if (length $in > 80 || $in !~ /^[-\w ]*$/) {
        app->log->debug($in);
        $c->render(text => "Query too long or wrong format", status => 400);
        return;
    }
    $c->render(json => [ search_html($c->stash('query')) ]);
};

post 'start' => sub {
    my $c = shift;
    my $data = $c->req->json;
    unless (
        defined $data->{magnet} &&
        length $data->{magnet} < 1000 &&
        $data->{magnet} =~ /^magnet:\?/
    ) {
        $c->render(text => "No magnet link in request", status => 400);
        return;
    }
    my $res = system "transmission-remote -a $data->{magnet}";
    my $code = $res ? 500 : 201;
    $c->render(text => "transmission-remote exited $res", status => $code);
};

# Run webapp or CLI
if ($daemon) {
    app->start('daemon', '-l', "http://*:$port");
} else {
    my $magnet = cli() or die "No hits or selection\n";
    system "transmission-remote -a $magnet";
}

# Helpers
sub cli {
    $|++;
    return unless $query;
    my @results = search_html($query);
    my $i = 0;
    print "No  SEEDS    SIZE  TITLE\n";
    print map {
        sprintf "%2d %6d %7s  %s\n", $i++, $_->{seeds}, $_->{size}, $_->{title}
    } @results;
    print "Enter No of torrent to start: ";
    $i = <>;
    chomp $i;
    return unless $i =~ /^\d+$/ && $i < @results;
    return $results[$i]->{magnet};
}

sub search_rss {
    my $query = shift;
    my $url = sprintf $urls{rss}, $query;
    my $rss = $ua->get($url)->res->body;
    my $dom = Mojo::DOM->new($rss);

    return sort { $b->{seeds} <=> $a->{seeds} } map { {
        title => $_->children('title')->first->content,
        magnet => $_->children('torrent\\:magnetURI')->first->content =~ /!\[CDATA\[(.+)\]\]/,
        seeds => $_->children('torrent\\:seeds')->first->content,
        size => human_size($_->children('torrent\\:contentLength')->first->content),
    } } @{ $dom->find('item') };
}

sub search_html {
    my $query = join '+', split(/\s/, shift);
    my $url = sprintf $urls{html}, $query;
    my $html = $ua->get($url)->res->body;
    return sort { $b->{seeds} <=> $a->{seeds} } map {
        my ($magnet, $title) = /href="(magnet:.+?n=(.+?)&.+?)"/;
        my ($size, $unit) = m|<td class="nobr center"> (.+?)&nbsp;(.)iB .*?</td>|;
        $size .= $unit;
        my ($seeds) = m|<td class="green center">(\d+)</td>|;
        { title => $title, magnet => $magnet, seeds => $seeds, size => $size }
    } $html =~ m|<tr class="odd".+?</tr>|sg;
}

sub human_size {
    my $bytes = shift;
    my $i = 0;
    while ($bytes >= 1024) {
        $i++;
        $bytes /= 1024;
    }
    my $dec = $bytes < 10 ? 1 : 0;
    our @formats = ("%d", "%.${dec}fK", "%.${dec}fM", "%.${dec}fG", "%.${dec}fT");
    return sprintf($formats[$i], $bytes);
}

__DATA__

@@ page.html.ep
<!DOCTYPE html>
<html>
<head>
<meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=0"/>
<meta name="apple-mobile-web-app-capable" content="yes" />
<link href="favicon.png" rel="icon" />
<link href="favicon.png" rel="shortcut icon" />
<link rel="apple-touch-icon" href="webclip-icon.png" />
<link rel="stylesheet" href="style.css">
<script src="kat.js"></script>
<title>kat-get <%= $title %></title>
</head>
<body onload="kat.focusQuery()">
<div class="bar" id="toolbar">
<form name="search">
kat-get
<input type="text" id="query" size="30" maxlength="80"
onkeydown="if (event.keyCode == 13) { kat.search(this.value); return false; }">
</form>
</div>
<div id="hits"></div>
<div class="bar" id="status">Welcome to kat-get</div>
</body>
</html>
